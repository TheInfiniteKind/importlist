import java.util.jar.Attributes

import java.text.SimpleDateFormat

import org.apache.commons.configuration.Configuration;
import org.apache.commons.configuration.PropertiesConfiguration

import proguard.gradle.ProGuardTask


apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile(':moneydance:')
    compile('com.brsanthu:google-analytics-java:1.1.2')
    compile('commons-io:commons-io:2.5')
    compile('org.apache.commons:commons-lang3:3.6')
    compile('commons-configuration:commons-configuration:1.10')
    compile('org.slf4j:slf4j-api:1.7.25')
    compile('org.slf4j:slf4j-nop:1.7.25')
    compile('commons-logging:commons-logging:1.2')
    compile('org.checkerframework:checker:2.2.0')
}

jar {
    baseName rootProject.name
    manifest {
        attributes ((Attributes.Name.IMPLEMENTATION_TITLE.toString()): getConfigValueForKey("module_name"),
                    (Attributes.Name.IMPLEMENTATION_VERSION.toString()): getConfigValueForKey("module_build"),
                    (Attributes.Name.IMPLEMENTATION_VENDOR.toString()): getConfigValueForKey("vendor"),
                    'Built-By': System.getProperty('user.name'),
                    'Built-Date': getCurrentTimestamp(),
                    'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')')

    }
}

assemble.dependsOn shadowJar
build.dependsOn shadowJar

shadowJar {
    baseName rootProject.name
    from 'NOTICE'
    from 'LICENSE'
    exclude 'META-INF/**/*'

    dependencies {
        include dependency('commons-lang:commons-lang:2.6')
        include dependency('org.apache.commons:commons-lang3:3.6')
        include dependency('commons-io:commons-io:2.5')
        include dependency('commons-configuration:commons-configuration:1.10')
        include dependency('org.apache.httpcomponents:httpcore:4.3')
        include dependency('org.apache.httpcomponents:httpclient:4.3')
        include dependency('commons-logging:commons-logging:1.2')
        include dependency('com.brsanthu:google-analytics-java:1.1.2')
        include dependency('org.slf4j:slf4j-api:1.7.25')
        include dependency('org.slf4j:slf4j-nop:1.7.25')
    }
    relocate 'org.apache.http', 'com.moneydance.modules.features.importlist.apache.http'
}

task proguard(type: ProGuardTask, dependsOn: 'shadowJar') {
    injars  shadowJar.archivePath
    outjars "${project.distsDir}/proguard-${rootProject.name}.jar"
    configuration 'config/proguard.cfg'
}

proguard.doLast {
    new File("${project.distsDir}/proguard-${rootProject.name}.jar").renameTo mxtfile as String
}

shadowJar.finalizedBy proguard

javadoc {
    options.author = true
    options.version = true
    options.memberLevel = JavadocMemberLevel.PACKAGE
    options.use = true
    options.linkSource = true
    options.docTitle = 'Import List for Moneydance'
    options.windowTitle = 'Import List for Moneydance'
    options.links = [
        'https://docs.oracle.com/javase/7/docs/api/',
        'http://moneydance.com/dev/apidoc/',
        'https://brsanthu.github.io/google-analytics-java/javadocs/',
        'http://www.slf4j.org/apidocs/',
        'https://commons.apache.org/proper/commons-configuration/javadocs/v1.10/apidocs/',
        'https://commons.apache.org/proper/commons-io/javadocs/api-2.5/',
        'https://commons.apache.org/proper/commons-lang/javadocs/api-3.6/',
        'https://commons.apache.org/proper/commons-lang/javadocs/api-2.6/',
        'https://commons.apache.org/proper/commons-logging/javadocs/api-release/'
    ]
    options.footer = '<a href="https://github.com/my-flow/importlist" target="_blank">Import List on GitHub</a>'
}

String getCurrentTimestamp() {
  Date today = new Date()
  SimpleDateFormat df = new SimpleDateFormat('yyyy-MM-dd HH:mm:ss')
  return df.format(today)
}

String getConfigValueForKey(String key) {
    file('src/main/resources/com/moneydance/modules/features/importlist/meta_info.dict').withInputStream {
        final Configuration config = new PropertiesConfiguration();
        config.load(it);
        return config.getString("\"" + key + "\"").replaceAll("^\"|\"\$", "");
    }
}